<motif-seqlet-cutoffs-configuration> ::=
    {<seqlet-scanning-settings>,
    <verbosity-section>}

<seqlet-scanning-settings> ::=
    <seqlet-tsv-section>
    "modisco-h5" : <file-name>,
    <seqlet-contrib-section>
    <pattern-spec-section>,
    <quantile-cutoff-section>,
    "trim-threshold" : <number>,
    "trim-padding" : <integer>,
    "background-probs" : <vector-or-genome>,
    <quantile-json-section>

<quantile-cutoff-section> ::=
    "seq-match-quantile" : <number-or-null>,
    "contrib-match-quantile" : <number-or-null>,
    "contrib-magnitude-quantile" : <number-or-null>

<pattern-spec-section> ::=
    "patterns" : "all"
  | "patterns" : [<list-of-pattern-specs>]

<list-of-pattern-specs> ::=
    <pattern-spec>
  | <pattern-spec>, <list-of-pattern-specs>

<pattern-spec> ::=
    {<optional-quantile-cutoff-section>
     "metacluster-name" : <string>,
     <pattern-option>}

<pattern-option> ::=
     "pattern-name" : <string>
  |  "pattern-name" : <string>,
     "short-name" : <string>}
  |  "pattern-names" : [<list-of-string>]
  |  "pattern-names" : [<list-of-string>],
     "short-names" : [<list-of-string>]

<optional-quantile-cutoff-section> ::=
    <quantile-cutoff-section>,
  | <empty>

<vector-or-genome> ::=
    "danRer11" | "hg38" | "dm6" | "mm10" | "sacCer3"
  | [<number>, <number>, <number>, <number>],

<quantile-json-section> ::=
    <empty>
  | "quantile-json" : <file-name>,

<seqlet-contrib-section> ::=
    <empty>
  | "modisco-contrib-h5" : <file-name>,

<seqlet-tsv-section> ::=
    <empty>
  | "seqlets-tsv" : <file-name>,
